#!/bin/csh

set sluschipath = `grep sluschipath ~/.sluschi.rc | cut -d'=' -f2`

if ( -e SLUSCHI_mds.out ) then
  mv SLUSCHI_mds.out SLUSCHI.out_last
endif

# Generate KPOINTS file
set kmesh = `getinfo.csh kmesh job.in`
if ( $kmesh > 0 ) then
cat - << EOF > KPOINTS
KPOINTS by SLUSCHI
0
Auto
$kmesh
EOF
cp KPOINTS KPOINTS_OPT
else
cat - << EOF > KPOINTS
KPOINTS by SLUSCHI
1
R
0.25 0.25 0.25 1
EOF
cat - << EOF > KPOINTS_OPT
KPOINTS by SLUSCHI
0
Auto
20
EOF
endif

if ( -d src_coex ) rm -r src_coex
mkdir src_coex
mkdir src_coex/anal
mkdir src_coex/anal/history

cat - << EOF >> SLUSCHI_mds.out
====================================================================================================
Solid and Liquid in Ultra Small Coexistence with Hovering Interfaces (SLUSCHI)
by Qi-Jun Hong and Axel van de Walle
EOF

echo "====================================================================================================" >> SLUSCHI_mds.out
echo Reading job.in...
getinfo.csh intpol job.in > /dev/null
if ( $? == 0 ) then
  @ intpol = `getinfo.csh intpol job.in`
  if ( $intpol == 1 ) then
    echo You choose to interpolate the value for TAG temp >> SLUSCHI_mds.out
    $sluschipath/intpol >> SLUSCHI_mds.out
  endif
endif

echo "====================================================================================================" >> SLUSCHI_mds.out
if ( ! -e Dir_OptUnitCell ) then
  mkdir Dir_OptUnitCell
  cd Dir_OptUnitCell
  $sluschipath/optunitcell >> log.out &
  echo \*\*\* Unit-cell optimization is running. Please check it is still running in Dir_OptUnitCell. \*\*\* >> ../SLUSCHI_mds.out
  echo \*\*\* Please read Dir_OptUnitCell/log.out for details. \*\*\* >> ../SLUSCHI_mds.out
  sleep 5
  echo === Initial Lattice Vectors === >> ../SLUSCHI_mds.out
  head -5 POSCAR | tail -4 >> ../SLUSCHI_mds.out
else
  cd Dir_OptUnitCell
endif

# in Dir_OptUnitCell
@ stop = 0
while ( $stop == 0 )
  if ( -e optunitcell_is_completed || -e optunitcell_is_done ) then
    @ stop = 1
    echo \*\*\* I find that unit-cell optimization completed. \*\*\* >> ../SLUSCHI_mds.out
    echo === Optimized Lattice Vectors === >> ../SLUSCHI_mds.out
    head -5 CONTCAR | tail -4 >> ../SLUSCHI_mds.out
  else
    sleep 60
    find .. -name job.in | grep Dir | xargs -n1 -i cp ../job.in {} > /dev/null
  endif
end # optunitcell is completed
cd ..

echo "====================================================================================================" >> SLUSCHI_mds.out
getinfo.csh radius job.in > size.in
#$sluschipath/solid.x >> SLUSCHI_mds.out
#cat POSCAR_HEADER POSCAR_STRCT > POSCAR

echo "====================================================================================================" >> SLUSCHI_mds.out
if ( ! -e Dir_VolSearch ) then
  mkdir Dir_VolSearch
  cd Dir_VolSearch
#  $sluschipath/volsearch >> log.out &
  echo \*\*\* Volume search is running. Please check it is still running in Dir_VolSearch \*\*\* >> ../SLUSCHI_mds.out
  echo \*\*\* Please read Dir_VolSearch/log.out for details. \*\*\* >> ../SLUSCHI_mds.out
else
  cd Dir_VolSearch
endif

# in Dir_VolSearch
@ stop = 0
while ( $stop == 0 )
  if ( -d 10 ) then
    sed -i '/adj_potim/d' ../job.in
    echo adj_potim = 0 >> ../job.in
  endif
  if ( -e n_iter_exclude ) then
    @ n_exclude = `cat n_iter_exclude`
  else
    @ n_exclude = 50
  endif
  if ( -d $n_exclude ) then
    cd ../
    $sluschipath/mds_src/master.csh
    $sluschipath/mds_src/collect.csh >> SLUSCHI_mds.out
    cd -
  endif
  find .. -name job.in | grep Dir | xargs -n1 -i cp ../job.in {} > /dev/null
  if ( -e volsearch_is_completed || -e volsearch_is_done ) then
    @ stop = 1
    echo \*\*\* I find that the volume search completed. \*\*\* >> ../SLUSCHI_mds.out
    #echo === Initial Lattice Vectors === >> ../SLUSCHI_mds.out
    #head -5 1/POSCAR | tail -4 >> ../SLUSCHI_mds.out
    #echo === Optimized Lattice Vectors === >> ../SLUSCHI_mds.out
    #head -5 CONTCAR | tail -4 >> ../SLUSCHI_mds.out
  endif
  sleep 600
end # volsearch is completed
cd ..

@ only = 0
getinfo.csh thmexp_only job.in > /dev/null
if ( $? == 0 ) then
  set only = `getinfo.csh thmexp_only job.in`
endif
if ( $only == 1 ) then
  echo \*\*\* Thermal expansion ONLY. Stopping code... \*\*\*
  exit
endif

