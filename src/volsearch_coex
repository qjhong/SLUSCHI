#!/bin/csh

echo \*\*\* This is the first simulation at this temperature. Volume search is running... \*\*\*

# in $temp_now/$temp_now/Dir_VolSearchCoex
cp ../../../../job.in .
set sluschipath = `grep sluschipath ~/.sluschi.rc | cut -d'=' -f2`
set vaspcmd = `getinfo.csh vaspcmd job.in`

set temp = `grep TEEND INCAR | cut -d'=' -f2`
$sluschipath/UpdateINCAR NSW 80


# submit the first job
set nstep=1
# initialize flags
@ stopflag = 0
@ count = 0

getinfo.csh mlff job.in > /dev/null                                             
if ( $? == 0 ) then                                                             
  @ mlff = `getinfo.csh mlff job.in`                                            
else                                                                            
  @ mlff = 0                                                                    
endif                                                                           
if ( $mlff == 1 ) then                                                          
$sluschipath/UpdateINCAR ML_LMLFF .TRUE.                                        
$sluschipath/UpdateINCAR ML_ISTART 0                                            
$sluschipath/UpdateINCAR ML_MHIS 5                                              
$sluschipath/UpdateINCAR ALGO N                                                 
endif                                                                           



$vaspcmd jobsub >> jobsub.log
@ flag_gamma = 0

while ($stopflag == 0)

  sleep 60 ############################################################

  if ( -e OUTCAR ) then
  grep -s "Total CPU" OUTCAR > /dev/null
  if (! $?) then # Job is completed

    echo ====== JOB $nstep DONE ======

    if ( $count == 0 ) then
      echo --- Calculate Pulay stress... ---
      $sluschipath/PressurePulay > /dev/null
    endif
    @ count = $count + 1
    if ( $count == 10 ) @ count = 0
    set pressure_Pulay = `cat pressure_Pulay.out`

    $sluschipath/GetPTE # get P, T and E
    grep 'in kB' OUTCAR -B1 | grep -v 'in kB' | grep -v ' 0.00 ' | grep -v '\-\-' | grep -v CPU | grep -v cpu | awk '{print $2,$3,$4,$5,$6,$7}' > pressure3.out # get Pxx, Pyy, Pzz, Pxy, Pyz, Pzx
    @ nlines = `cat pressure3.out | wc -l`
    @ nlines = `awk 'BEGIN{print ('$nlines'-'$nlines'%2)/2}'`
    tail -$nlines pressure3.out > pressure3.out_tmp
    mv pressure3.out_tmp pressure3.out
    set volume=`grep volume/ion OUTCAR | awk '{print $5}'`
    set pressure_kinetic=`awk -v k="1.38e-23" -v a="1.0e22" -v b=$temp -v c=$volume 'BEGIN{print (k*b/c*a)}'`
    echo $pressure_kinetic > pressure_kinetic.out
    grep 'volume of cell' OUTCAR | awk '{print $5}' >> volume.out

    set pressure_target = `getinfo.csh press job.in`
    echo $pressure_target  > pressure_target.out
    
    $sluschipath/PressureAvg.x 

    head -5 POSCAR | tail -3 > lattice.out
    set v1 = `head -1 lattice.out`
    set v2 = `head -2 lattice.out | tail -1`
    set v3 = `head -3 lattice.out | tail -1`
    rm lattice.out
    set p = `cat pressure3_total.out`
    echo $v1 $v2 $v3 $p >> lattice_pressure_history.out

    set pxx = `echo $p | awk '{print $1}'`
    set pyy = `echo $p | awk '{print $2}'`
    set pzz = `echo $p | awk '{print $3}'`
    set pxy = `echo $p | awk '{print $4}'`
    set pyz = `echo $p | awk '{print $5}'`
    set pxz = `echo $p | awk '{print $6}'`
    echo "--- LATTICE VECTORS --- \t\t --- PRESSURE ---"
    echo "$v1 \t $pxx $pxy $pxz" 
    echo "$v2 \t $pxy $pyy $pyz"
    echo "$v3 \t $pxz $pyz $pzz"
    echo --- PRESSURE ---
    echo Pulay stress $pressure_Pulay, kinetic pressure $pressure_kinetic, pressure target $pressure_target kbar.
    echo TOTAL: $p kbar

    set navg_pressure = `getinfo.csh navg job.in`
    set factor_divide = `getinfo.csh factor job.in`
    echo 1 > param.in # seems "1" is better/faster to achieve correct volume
    echo $factor_divide >> param.in
    $sluschipath/DetermineSize.x

    getinfo.csh thmexp_cnvg job.in > /dev/null
    if ( $? == 0 ) then
      getinfo.csh thmexp_cnvg job.in > thmexp.in
    else
      echo 20.0 > thmexp.in
    endif
    $sluschipath/VolSearchStop.x
    @ stopflag = `cat VolSearchStop.out`
    if ( $nstep >= 5 ) @ stopflag = 1
    if ( $stopflag == 1 ) then
    else
      echo --- ADJUST INCAR TAGS ---
      $sluschipath/AdjustNBANDS
      $sluschipath/AdjustPOTIM
      $sluschipath/AdjustBMIX
      mkdir $nstep
      cp INCAR KPOINTS POSCAR OSZICAR $nstep
      @ NKPTS = `grep NKPTS OUTCAR | awk '{print $4}'`
      @ gamma = `getinfo.csh gamma_possible job.in`
      mv OUTCAR $nstep
      touch OUTCAR
      cp POSCAR POSCAR_tmp
      head -2 CONTCAR > POSCAR
      cat lattice_predict.out >> POSCAR
      tail -n+6 POSCAR_tmp >> POSCAR
      rm WAVECAR
      if ( -e ML_ABN ) then                                                     
          cp ML_ABN ML_AB                                                       
          $sluschipath/UpdateINCAR ML_ISTART 1                                  
      endif
      if ( $NKPTS == 1 && $gamma == 1 && -e ../../../../jobsub_gamma ) then
        if ( $flag_gamma == 0 ) then
          cp IBZKPT KPOINTS
          rm WAVECAR
          @ flag_gamma = 1
        endif
        cp ../../../../jobsub_gamma jobsub
      else
        cp ../../../../jobsub .
      endif
      $vaspcmd jobsub >> jobsub.log
      set nstep = `expr $nstep + 1`
      echo Job $nstep submitted. Waiting for job $nstep to finish... 
    endif
  endif
  endif
end
cp INCAR ../*0/INCAR
cp INCAR ../../src_coex/INCAR
echo \*\*\* total pressure is $p kbar, which meets the target $pressure_target within 20 kbar. \*\*\* 
echo \*\*\* VOLUME SEARCH IS DONE \*\*\*
echo stoping script...
echo "----------------------------------------------------------------------------------------------------"
